# Create Security Group
resource "aws_security_group" "start_up_sg" {
  vpc_id = aws_vpc.start_up_vpc.id
  tags = {
    Name = "start-up-sg"
  }
}

# Ingress Rules
resource "aws_vpc_security_group_ingress_rule" "ssh_ingress" {
  security_group_id = aws_security_group.start_up_sg.id
  from_port         = 22
  to_port           = 22
  ip_protocol       = "tcp"
  cidr_ipv4         = "0.0.0.0/0"
}

# If you have specifics for the in-bound rules, then specify them.
# Avoid this!
resource "aws_vpc_security_group_ingress_rule" "all_tcp_ingress" {
  security_group_id = aws_security_group.start_up_sg.id
  from_port         = 0
  to_port           = 65535
  ip_protocol       = "tcp"
  cidr_ipv4         = "0.0.0.0/0"
}

# Egress Rules
resource "aws_vpc_security_group_egress_rule" "all_egress" {
  security_group_id = aws_security_group.start_up_sg.id
  from_port         = 0
  to_port           = 0
  ip_protocol       = "-1"
  cidr_ipv4         = "0.0.0.0/0"
}

# Create EC2 Instances
resource "aws_instance" "start_up_instance" {
  count = var.instance_count

  ami           = "ami-0e2c8caa4b6378d8c" # Example AMI
  instance_type = "t3.medium"

  subnet_id                   = aws_subnet.start_up_subnet.id
  vpc_security_group_ids      = [aws_security_group.start_up_sg.id]
  associate_public_ip_address = true
  key_name                    = aws_key_pair.start_up_key.key_name

  # Root Volume (Default Storage)
  root_block_device {
    volume_size = 50
    volume_type = "gp3"
  }

  # Additional EBS Volume
  ebs_block_device {
    device_name           = "/dev/xvdb"
    volume_size           = 100
    volume_type           = "gp3"
    delete_on_termination = true
  }

  tags = {
    Name = "start-up-instance-${count.index + 1}"
  }
}

resource "tls_private_key" "pk" {
  algorithm = "RSA"
  rsa_bits  = 4096
}

resource "aws_key_pair" "start_up_key" {
  key_name   = "start-up-aws-key-pair"
  public_key = tls_private_key.pk.public_key_openssh
}

resource "local_file" "start_up_key" {
  content         = tls_private_key.pk.private_key_pem
  filename        = "./start-up-aws-key-pair.pem"
  file_permission = "0400"
} 
